plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 31
    buildToolsVersion "31.0.0"

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    useLibrary 'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 31
        versionCode 68
        versionName "3.3.1"
        renderscriptTargetApi 24

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "VERSION_NAME", "\"$versionName\""
        buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io\""
        buildConfigField "String", "FM_GPS_LAT_LONG", "\"48.12863302178715,11.572371166069702\""
    }

    buildTypes {
        debug {
            buildConfigField "String", "FM_API_BASE_URL", "\"https://api-dev.fantasmo.io\""
            minifyEnabled = false
            shrinkResources false
        }
        release {
            buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io\""
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            def appName = "FantasmoSDK"
            def versionNameValue = defaultConfig.versionName
            def buildType = variant.buildType.name
            def newName

            if (buildType == "debug") {
                newName = "${appName}_${versionNameValue}-${buildType}.aar"
            } else {
                newName = "${appName}_${versionNameValue}.aar"
            }

            outputFileName = newName
        }
    }

    afterEvaluate {
        publishing {
            publications {
                release(MavenPublication) {
                    from components.release

                    groupId = 'com.fantasmo'
                    artifactId = 'fantasmosdk'
                    version = defaultConfig.versionName
                }

                debug(MavenPublication) {
                    from components.debug

                    groupId = 'com.fantasmo'
                    artifactId = 'fantasmosdk'
                    version = defaultConfig.versionName
                }
            }
            repositories {
                maven {
                    name = "GitHubPackages"
                    url = "https://maven.pkg.github.com/fantasmoio/Fantasmo-Android-SDK"
                    credentials {
                        username = project.findProperty("gpr.user") ?: System.getenv("GITHUB_USERNAME")
                        password = project.findProperty("gpr.key") ?: System.getenv("GITHUB_TOKEN")
                    }
                }
            }
        }
    }
    aaptOptions {
        noCompress "tflite"
        noCompress "lite"
    }
}
 dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'com.android.volley:volley:1.2.1'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.6.1-native-mt'
    implementation 'com.google.code.gson:gson:2.9.0'

    // ARCore
    implementation 'com.google.ar:core:1.31.0'

    // Location Manager
    implementation 'com.google.android.gms:play-services-location:19.0.1'

    // Barcode model dependencies
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    // TensorFlow Lite
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.4.0'
    implementation 'org.tensorflow:tensorflow-lite-gpu:2.8.0'

    // Layouts
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.2.0'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.1-native-mt'
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.robolectric:robolectric:4.8.1'
    testImplementation 'org.mockito:mockito-core:4.5.1'
    testImplementation 'org.mockito:mockito-inline:4.5.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
