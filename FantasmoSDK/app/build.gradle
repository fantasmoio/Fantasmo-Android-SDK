plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    testOptions {
        unitTests {
            includeAndroidResources = true
            returnDefaultValues = true
        }
    }

    useLibrary  'org.apache.http.legacy'

    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 30
        versionCode 20
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io/v1/image.localize\""
        buildConfigField "String", "FM_GPS_LAT_LONG", "\"48.12863302178715,11.572371166069702\""
    }

    buildTypes {
        debug{
            minifyEnabled = false
            shrinkResources false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    flavorDimensions "env"
    productFlavors {
        prod{
            dimension "env"
        }
        devParis {
            dimension "env"
            buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io/v1/image.localize\""
            buildConfigField "String", "FM_GPS_LAT_LONG", "\"48.848138681935886,2.371750713292894\""
        }
        devMunich {
            dimension "env"
            buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io/v1/image.localize\""
            buildConfigField "String", "FM_GPS_LAT_LONG", "\"48.12863302178715,11.572371166069702\""
        }
        devMiami{
            dimension "env"
            buildConfigField "String", "FM_API_BASE_URL", "\"https://api.fantasmo.io/v1/image.localize\""
            buildConfigField "String", "FM_GPS_LAT_LONG", "\"25.762586765198417,-80.19404801110545\""
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            def appName = "FantasmoSDK"
            def versionNameValue = defaultConfig.versionName
            def buildType = variant.buildType.name
            def newName

            if (buildType == "debug") {
                newName = "${appName}_${versionNameValue}-${buildType}.aar"
            } else {
                newName = "${appName}_${versionNameValue}.aar"
            }

            outputFileName = newName
        }
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'com.android.volley:volley:1.2.0'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.8'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.ar:core:1.23.0'
    implementation 'com.google.ar.sceneform.ux:sceneform-ux:1.17.1'
    implementation 'com.google.android.gms:play-services-location:18.0.0'

    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.5.0'
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.robolectric:robolectric:4.5.1"
    testImplementation "org.mockito:mockito-core:3.8.0"
    testImplementation 'org.mockito:mockito-inline:2.13.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
