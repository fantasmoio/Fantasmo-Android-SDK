plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'com.google.android.libraries.mapsplatform.secrets-gradle-plugin'
}

android {
    signingConfigs {
        fantasmo {
            storeFile file('../certificate')
            storePassword 'fantasmo'
            keyAlias 'fantasmo'
            keyPassword 'fantasmo'
        }
    }
    compileSdkVersion 31

    defaultConfig {
        applicationId "com.example.fantasmo_android"
        minSdkVersion 19
        targetSdkVersion 31
        versionCode 2
        versionName "2.0"
        multiDexEnabled = true

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        signingConfig signingConfigs.fantasmo
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.fantasmo
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        viewBinding true
    }

    applicationVariants.all { variant ->
        variant.outputs.all {
            def appName = "Fantasmo_SDK_Demo_App"
            def buildType = variant.buildType.name
            def newName

            if (buildType == "debug") {
                newName = "${appName}_${variant.versionName}-${buildType}.apk"
            } else {
                newName = "${appName}_${variant.versionName}.apk"
            }

            outputFileName = newName
        }
    }

    aaptOptions {
        noCompress "tflite"
        noCompress "lite"
    }
}

dependencies {
    // Include libs folder
    implementation fileTree(dir: 'libs', include: ['*.aar'])

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'com.google.android.material:material:1.6.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    // Fragment Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.4.2'

    // Google ARCore
    implementation 'com.google.ar:core:1.31.0'

    // Location Services
    implementation 'com.google.android.gms:play-services-location:19.0.1'
    implementation 'com.google.android.gms:play-services-maps:18.0.2'

    // Barcode model dependencies
    implementation 'com.google.mlkit:barcode-scanning:17.0.2'

    //GSON for JSON parse
    implementation 'com.google.code.gson:gson:2.9.0'
    implementation 'com.android.volley:volley:1.2.1'

    // Tensor
    implementation 'org.tensorflow:tensorflow-lite-support:0.4.0'
    implementation 'org.tensorflow:tensorflow-lite-metadata:0.4.0'

    implementation 'org.tensorflow:tensorflow-lite-gpu:2.8.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}